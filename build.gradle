plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'com.diffplug.eclipse.apt' version '4.0.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

repositories {
    maven {
        name 'Su5ed LegacyDev Fork'
        url 'https://maven.su5ed.dev/releases'
    }
    
    maven {
        name 'Cleanroom Maven'
        url 'https://maven.cleanroommc.com'
    }
}

import net.minecraftforge.gradle.mcp.MCPRepo
import net.minecraftforge.gradle.common.tasks.SignJar
import net.minecraftforge.gradle.common.util.MavenArtifactDownloader
import org.apache.tools.ant.filters.ReplaceTokens

repositories {
    maven {
        url = 'https://www.cursemaven.com/'
        content {
            includeGroup "curse.maven"
        }
    }
}

version = "${mcversion}-${version}"
group = 'thecodex6824.thaumcraftfix'
archivesBaseName = 'ThaumcraftFix'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

ext {
    if (!project.hasProperty('certFingerprint')) {
        certFingerprint = ''    
    }
}

legacy {
    fixClasspath = true
    extractMappings = true
    attachMappings = true
}

final arch = System.getProperty("os.arch")
final needCustomNatives = arch != "x86" && arch != "i386" && arch != "x86_64" && arch != "amd64"

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    accessTransformer = file('src/main/resources/META-INF/thaumcraftfix_at.cfg')
    runs {
        client {
            workingDirectory file('run')
            
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            
            property 'fml.coreMods.load', 'thecodex6824.thaumcraftfix.core.ThaumcraftFixCore'

            if (needCustomNatives) {
                property 'java.library.path', ''
                environment 'nativesDirectory', "${project.getProjectDir()}/natives"
            }

            environment 'MC_VERSION', '1.12.2'
            
            mods {
                thaumcraftfix {
                    source sourceSets.main
                }
            }
        }
        
        server {
            workingDirectory file('run')
            
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            
            property 'fml.coreMods.load', 'thecodex6824.thaumcraftfix.core.ThaumcraftFixCore'

            if (needCustomNatives) {
                property 'java.library.path', ''
                environment 'nativesDirectory', "${project.getProjectDir()}/natives"
            }

            environment 'MC_VERSION', '1.12.2'
            
            mods {
                thaumcraftfix {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "${refmap}"
    
    checks = true
    debug.countInjections = true
    debug.strict = true
    debug.verbose = true
    env.remapRefMap = true
    overwriteErrorLevel = 'error'
    quiet
}

configurations {
    testCompile.extendsFrom compile
}

dependencies {
    minecraft "net.minecraftforge:forge:${mcversion}-${forgeversion}"
    
    implementation fg.deobf('curse.maven:thaumcraft-223628:2629023')
    implementation fg.deobf('curse.maven:baubles-227083:2518667')
    
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.+'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.+'
    
    runtimeOnly 'curse.maven:jei-238222:2995897'
    runtimeOnly 'curse.maven:thaumic-jei-285492:2705304'
    runtimeOnly 'curse.maven:the-one-probe-245211:2667280'
    runtimeOnly 'curse.maven:ctm-267602:2915363'
    
    annotationProcessor 'org.ow2.asm:asm-debug-all:5.2'
    annotationProcessor 'com.google.guava:guava:32.1.2-jre'
    annotationProcessor 'com.google.code.gson:gson:2.8.9'
    
    implementation("zone.rong:mixinbooter:${mixinbooterversion}") {
        transitive = false
    }
    annotationProcessor("zone.rong:mixinbooter:${mixinbooterversion}") {
        transitive = false
    }
    
    constraints {
        implementation('net.minecraftforge:legacydev:0.3.0.0-legacyExt') {
            because 'needed to have all ATs work properly'
        }
    
        implementation('org.ow2.asm:asm-debug-all:5.2') {
            because 'newer ASM versions include module info that causes errors in Java 8'
        }
    }
    
    configurations.forEach {
        // mergetool pollutes the classpath with conflicting classes
        it.exclude(group: 'net.minecraftforge', module: 'mergetool')
        
        // legacydev forge dep wants fatjar as the classifier, but 3.0 lacks it
        it.resolutionStrategy.eachDependency {
            if (it.requested.module.toString() == "net.minecraftforge:legacydev") {
                it.artifactSelection {
                    it.selectArtifact(DependencyArtifact.DEFAULT_TYPE, null, null)
                }
            }
        }
    }
    
    // For the native library binding jars (which need to be the exact same version as the natives)
    if (needCustomNatives) {
        implementation fileTree(dir: 'natives', include: '*.jar')
    }
}

idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
}

task filterTokens(type: Sync) {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', version
    inputs.property 'apiversion', apiversion
    inputs.property 'certFingerprint', certFingerprint
    
    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [APIVERSION: apiversion.toString(), VERSION: version.toString(), FINGERPRINT: certFingerprint.toString()])
    into "$buildDir/src/$sourceSets.main.name/$sourceSets.main.java.name"
}

compileJava.source = filterTokens.outputs

processResources {
    // required to allow file expansion later
    duplicatesStrategy = 'include'
    
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', version
    inputs.property 'apiversion', apiversion
    inputs.property 'mcversion', mcversion
    inputs.property 'forgeminversion', forgeminversion
    inputs.property 'mixinbooterminversion', mixinbooterminversion
    inputs.property 'refmap', refmap
    
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version': version, 'mcversion': mcversion, 'forgeversion': forgeversion, 'forgeminversion': forgeminversion
    }
    
    // do the same for all mixin config jsons
    from(sourceSets.main.resources.srcDirs) {
        include 'mixin/*.json'
        
        expand 'mixinbooterminversion': mixinbooterminversion, 'refmap': refmap
    }
        
    // copy everything else except everything handled above
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'mixin/*.json'
    }
}

javadoc {
    include 'thecodex6824/thaumcraftfix/api/**'
}

task signJar(type: SignJar) {
    dependsOn 'jar'
    onlyIf { project.hasProperty('keyStore') }
    
    if (project.hasProperty('keyStore')) { // needed to skip gradle trying to find vars
        keyStore = project.keyStore
        alias = project.alias
        storePass = project.storePass
        keyPass = project.keyPass
        
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'thaumcraftfix_at.cfg'
        attributes 'FMLCorePlugin': 'thecodex6824.thaumcraftfix.core.ThaumcraftFixCore'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

tasks.build.dependsOn(tasks.signJar)
tasks.test.dependsOn(tasks.createSrgToMcp)

project.tasks.whenTaskAdded {
    if (it.name.equals('reobfJar')) {
        tasks.signJar.dependsOn(it)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    archiveClassifier = 'javadoc'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    archiveClassifier = 'api'
    include 'thecodex6824/thaumcraftfix/api/**'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    archiveClassifier = 'sources'
}

artifacts {
    archives apiJar
    archives javadocJar
    archives sourcesJar
}

test {
    useJUnitPlatform()
    maxHeapSize = '1G'
}
